buildscript {
	ext {
		springBootVersion = '2.1.0.BUILD-SNAPSHOT'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
	}
	dependencies {
		classpath('com.google.cloud.tools:appengine-gradle-plugin:1.3.3')
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
  id "com.diffplug.gradle.spotless" version "3.15.0"
  id 'io.franzbecker.gradle-lombok' version '1.14'
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.google.cloud.tools.appengine'

group = 'com.gcp'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-web') {
		exclude(module: 'spring-boot-starter-tomcat')
	}
	implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
	implementation('org.springframework.boot:spring-boot-starter-data-jpa')
	implementation('com.h2database:h2')
	providedCompile('org.springframework.boot:spring-boot-starter-tomcat')
	runtimeOnly('org.springframework.boot:spring-boot-devtools')
	testImplementation('org.springframework.boot:spring-boot-starter-test')
}

eclipse {
	classpath {
		// Gradle のビルドパスと一致するように変更する
		file {
			beforeMerged { classpath ->
				classpath.entries.removeAll { it.kind == "src" }
			}
			whenMerged { classpath ->
				classpath.entries.findAll { it.path.startsWith("src/main/java") }*.output      = "build/classes/java/main"
				classpath.entries.findAll { it.path.startsWith("src/main/resources") }*.output = "build/resources/main"
				classpath.entries.findAll { it.path.startsWith("src/test/java") }*.output      = "build/classes/java/test"
				classpath.entries.findAll { it.path.startsWith("src/test/resources") }*.output = "build/resources/test"
			}
		}
	}
}

appengine {
	deploy {
		stopPreviousVersion = true	// default - stop the current version
		promote = true				// default - & make this the current version
	}
}

spotless {
	java {
		googleJavaFormat('1.6')
	}
}

lombok {
	version = '1.18.4'
	sha256 = ""
}